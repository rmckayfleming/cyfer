<program> := <whitespace?> expr*;
<expr> := <whitespace?> (sequence | atom);
<sequence> := form | tuple | map;
<atom> := nil | boolean | number | text | symbol | identifier; (* Notice the use of precedence, an identifier is pretty much anything else. *)

<exprs> := (<whitespace?> expr)*;
form := <'('> <whitespace?> exprs <')'>;
tuple := <'['> <whitespace?> exprs <']'>;
map := <'{'> <whitespace?> exprs <'}'>;

nil := <"#n"> | <"#nil">;

<boolean> := true | false;
true := <"#t"> | <"#true">;
false := <"#f"> | <"#false">;

(* Notice that we're using an ordered choice, integers have precedence over floats *)
<number> := float / ratio / integer;

<integer> := decimal-integer | binary-integer | hex-integer | octal-integer;
decimal-integer := #"[+-]?\d+";
binary-integer := #"[+-]?0b[01]+";
hex-integer := #"[+-]?0x\p{XDigit}+";
octal-integer := #"[+-]?0o[0-7]+";

ratio := #"[+-]?\d+" <"/"> #"\d+";

(* Todo: Extend this with other floating point types *)
float := #"[+-]?\d+" (float-fraction | float-exponent | float-fraction float-exponent);
<float-fraction> := "." #"\d+";
<float-exponent> := ("e" | "E") #"[+-]?\d+";

text := <'"'> quoted-text? <'"'>;
<quoted-text> := (escaped-character | #"[^\"\\]*")*;
escaped-character := "\\0" | "\\\\" | "\\t" | "\\n" | "\\r" | '\\"' | "\\u{" unicode-scalar-digits "}";
unicode-scalar-digits := #"\p{XDigit}{1,6}";

<symbol> := bare-symbol | quoted-symbol;
bare-symbol := <":"> identifier-characters* | identifier-characters* <":">;
quoted-symbol := <":"> <'"'> quoted-text? <'"'>;

(* Todo: Add escaped identifiers *)
identifier = !number identifier-characters;

whitespace = #"\s+"
<sign> = "-" | "+";
<identifier-characters> = #"[\p{ID_Continue}?!&=<>+-]+"